plugins {
    id "org.jetbrains.kotlin.js" version "1.3.61"
    id 'org.jetbrains.dokka' version '0.10.0'
    id "com.github.node-gradle.node" version "2.2.1"
    id 'maven-publish'
}

allprojects {
    group 'com.github.juggernaut0.kui'
    version '0.10.0'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
    testImplementation project(":kui-test")
}

task sourceJar(type: Jar) {
    from kotlin.sourceSets.main.kotlin
    classifier "sources"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.kotlin

            artifact sourceJar
        }
    }
    repositories {
        maven {
            name = "pages"
            url = uri("pages/m2/repository")
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.Kotlin2JsCompile) {
    kotlinOptions.moduleKind = "umd"
}

task populateNodeModules(type: Copy, dependsOn: compileTestKotlinJs) {
    configurations.testCompileClasspath.each {
        if (it.name.endsWith(".jar")) {
            from(zipTree(it.absolutePath).matching { include '*.js' })
        } else {
            from(fileTree(it.absolutePath).matching { include "*.js" })
        }
    }

    into "${buildDir}/node_modules"
}

node {
    download = true
    version = "12.14.1"
}

task installJest(type: NpmTask) {
    args = ['install', 'jest']
}

task runJest(type: NodeTask, dependsOn: [compileTestKotlinJs, populateNodeModules]) {
    script = file('node_modules/jest/bin/jest.js')
    args = [projectDir.toURI().relativize(compileTestKotlinJs.outputFile.toURI())]
}

test.dependsOn runJest
