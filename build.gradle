buildscript {
    ext.kotlin_version = '1.3.41'

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.dokka' version '0.10.0'
    id "com.moowork.node" version "1.3.1"
    id 'maven-publish'
}

apply plugin: 'kotlin2js'

group 'com.github.juggernaut0.kui'
version '0.9.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"
    testCompile "org.jetbrains.kotlin:kotlin-test-js"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar
        }
    }
    repositories {
        maven {
            name = "pages"
            url = uri("pages/m2/repository")
        }
    }
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "umd"
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

node {
    download = true
    version = "12.14.1"
}

task installJest(type: NpmTask) {
    args = ['install', 'jest']
}

task runJest(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules]) {
    script = file('node_modules/jest/bin/jest.js')
    args = [projectDir.toURI().relativize(compileTestKotlin2Js.outputFile.toURI())]
}

test.dependsOn runJest

project("example") {
    apply plugin: "kotlin2js"

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(":")
        compile "org.jetbrains.kotlin:kotlin-stdlib-js"
        testCompile "org.jetbrains.kotlin:kotlin-test-js"
    }

    task assembleWeb(type: Copy, dependsOn: classes) {
        configurations.compile.each { File file ->
            from(zipTree(file.absolutePath), {
                includeEmptyDirs = false
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js")
                }
            })
        }
        from compileKotlin2Js.destinationDir
        include '**/*.js'
        includeEmptyDirs false
        into "${projectDir}/build/web/js"
    }

    task copyStaticWeb(type: Copy) {
        from 'static'
        exclude 'test.html'
        into "${projectDir}/build/web"
    }

    assemble.dependsOn(assembleWeb, copyStaticWeb)
}
